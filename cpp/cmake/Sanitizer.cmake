if(CPP_SANITIZER)
  include(AddCompilerFlag)

  if(NOT (CPP_SANITIZER STREQUAL "Thread"))
    add_compiler_flag("-fno-omit-frame-pointer")
  endif()

  macro(append_address_sanitizer_flags)
    add_compiler_flag("-fsanitize=address")
    add_compiler_flag("-fsanitize-address-use-after-scope")
    add_compiler_flag("-fno-optimize-sibling-calls")
  endmacro()

  macro(append_undefined_sanitizer_flags)
    add_compiler_flag("-fsanitize=undefined")
    add_compiler_flag("-fno-sanitize-recover=all")

    if(CMAKE_CXX_COMPILER_ID MATCHES "(Apple)?Clang")
      add_compiler_flag("-fsanitize=float-divide-by-zero")
      add_compiler_flag("-fsanitize=unsigned-integer-overflow")
      add_compiler_flag("-fsanitize=implicit-conversion")
      add_compiler_flag("-fsanitize=local-bounds")
      add_compiler_flag("-fsanitize=nullability")
      add_compiler_flag("-fsanitize-recover=unsigned-integer-overflow")
    endif()
  endmacro()

  if(CPP_SANITIZER STREQUAL "Address")
    message(STATUS "Build with AddressSanitizer")

    if(CMAKE_CXX_COMPILER_ID STREQUAL "AppleClang")
      message(WARNING "AppleClang does not support LeakSanitizer")
    endif()

    append_address_sanitizer_flags()
  elseif(CPP_SANITIZER STREQUAL "Thread")
    message(STATUS "Build with ThreadSanitizer")
    add_compiler_flag("-fsanitize=thread")
  elseif(CPP_SANITIZER STREQUAL "Memory")
    if(CMAKE_COMPILER_IS_GNUCXX)
      message(FATAL_ERROR "GCC does not support MemorySanitizer")
    endif()

    if(APPLE)
      message(FATAL_ERROR "macOS does not support MemorySanitizer")
    endif()

    message(STATUS "Build with MemorySanitizer")

    add_compiler_flag("-fsanitize=memory")
    add_compiler_flag("-fsanitize-memory-track-origins")
    add_compiler_flag("-fno-optimize-sibling-calls")
  elseif(CPP_SANITIZER STREQUAL "Undefined")
    message(STATUS "Build with UndefinedSanitizer")
    append_undefined_sanitizer_flags()
  elseif(CPP_SANITIZER STREQUAL "Address;Undefined")
    message(STATUS "Build with AddressSanitizer and UndefinedSanitizer")
    append_address_sanitizer_flags()
    append_undefined_sanitizer_flags()
  else()
    message(FATAL_ERROR "The Sanitizer is not supported: ${CPP_SANITIZER}")
  endif()
endif()
